Sandstorm.NeosTwoFactorAuthentication.BackendController.new = Sandstorm.NeosTwoFactorAuthentication:Page.DefaultPage {
    body = Sandstorm.NeosTwoFactorAuthentication:BodyLayout.Default {
        flashMessages = ${flashMessages}

        teaserTitle = ${I18n.id('module.new.title').package('Sandstorm.NeosTwoFactorAuthentication').source('Backend').translate()}
        teaserText = ${I18n.id('module.new.description').package('Sandstorm.NeosTwoFactorAuthentication').source('Backend').translate()}

        content = Neos.Fusion:Component {
            renderer = afx`
            <Neos.Fusion.Form:Form form.target.action="create">
                <Neos.Fusion.Form:Hidden field.name="secret" field.value={secret}/>

                <div class="neos-control-group">
                    <img src={qrCode} style="width: 100%; max-width: 400px"/>
                </div>

                <div class="neos-control-group">
                    <Neos.Fusion.Form:Input
                        field.name="secondFactorFromApp"
                        attributes.required="required"
                        attributes.id="secondFactorFromApp"
                        attributes.placeholder={I18n.id('otp-placeholder').package('Sandstorm.NeosTwoFactorAuthentication')}
                        attributes.class="neos-span5"
                        attributes.aria-label={I18n.id('otp-placeholder').package('Sandstorm.NeosTwoFactorAuthentication')}
                        attributes.autocomplete="off"
                    />
                </div>

                <div class="neos-control-group">
                    <Neos.Fusion.Form:Button>
                        {I18n.id('module.new.submit-otp').package('Sandstorm.NeosTwoFactorAuthentication').source('Backend').translate()}
                    </Neos.Fusion.Form:Button>
                </div>
            </Neos.Fusion.Form:Form>
            `
        }
    }
}
