# you need to include ALL packages you want to use here, there is no AutoInclude!
include: resource://Neos.Fusion/Private/Fusion/Root.fusion
include: resource://Neos.Fusion.Form/Private/Fusion/Root.fusion

Sandstorm.NeosTwoFactorAuthentication.SecondFactorCreationController.index = Neos.Fusion:Component {
    site = null
    flashMessages = ${flashMessages}

    renderer = Neos.Fusion:Join {
        doctype = '<!DOCTYPE html>'
        doctype.@position = 'start 100'

        content = Neos.Fusion:Component {
            @apply.props = ${props}

            backgroundImageSource = Neos.Fusion:Component {
                @if.set = ${this.path}
                path = ${Configuration.setting('Neos.Neos.userInterface.backendLoginForm.backgroundImage')}

                renderer = Neos.Fusion:Case {
                    resourcePath {
                        condition = ${String.indexOf(props.path, 'resource://') == 0}
                        renderer = Neos.Fusion:ResourceUri {
                            path = ${props.path}
                        }
                    }

                    default {
                        condition = true
                        renderer = ${props.path}
                    }
                }
            }

            stylesheetUri = Neos.Fusion:Tag {
                tagName = 'link'
                attributes {
                    rel = 'stylesheet'
                    href = Neos.Fusion:ResourceUri {
                        path = 'resource://Neos.Neos/Public/Styles/Login.css'
                    }
                }
            }

            backgroundImageSourceIsWebp = ${this.backgroundImageSource ? String.endsWith(this.backgroundImageSource, '.webp') : null}

            headerComment = ${Configuration.setting('Neos.Neos.headerComment')}

            teaserTitle = ${I18n.id('module.new.title').package('Sandstorm.NeosTwoFactorAuthentication').source('Backend').translate()}
            teaserText = ${I18n.id('module.new.description').package('Sandstorm.NeosTwoFactorAuthentication').source('Backend').translate()}

            severityMapping = Neos.Fusion:DataStructure {
                OK = 'success'
                Notice = 'notice'
                Warning = 'warning'
                Error = 'error'
            }

            renderer = afx`
                <html lang="en">
                {props.headerComment}
                <head>
                    <meta charset="utf-8"/>
                    <meta name="robots" content="noindex"/>
                    <title>{props.teaserTitle}</title>

                    {props.stylesheetUri}
                    <link href={StaticResource.uri('Neos.Neos', 'Public/Images/Icons/apple-touch-icon.png')}
                          sizes="180x180" rel="apple-touch-icon"/>
                    <link href={StaticResource.uri('Neos.Neos', 'Public/Images/Icons/favicon-16x16.png')} sizes="16x16"
                          rel="icon" type="image/png"/>
                    <link href={StaticResource.uri('Neos.Neos', 'Public/Images/Icons/favicon-32x32.png')} sizes="32x32"
                          rel="icon" type="image/png"/>
                    <link href={StaticResource.uri('Neos.Neos', 'Public/Images/Icons/safari-pinned-tab.svg')}
                          rel="mask-icon" color="#00adee"/>

                    <style type="text/css" @if.set={props.backgroundImageSource && !props.backgroundImageSourceIsWebp}>
                        {'body.neos--bg,.neos-login-box:before{background-image:url(' + props.backgroundImageSource + ')}'}
                    </style>
                    <style type="text/css" @if.set={props.backgroundImageSource && props.backgroundImageSourceIsWebp}>
                        {'.webp body.neos--bg,.webp .neos-login-box:before {background-image: url(' + props.backgroundImageSource + ')}'}
                        {'.no-webp body.neos--bg,.no-webp .neos-login-box:before {background-image: url(' + String.substr(props.backgroundImageSource, 0, String.length(props.backgroundImageSource) - 4) + 'jpg)}'}
                    </style>
                </head>
                <body class={["neos", props.backgroundImageSource ? 'neos--bg' : null]} style="">
                <img @if.set={props.backgroundImageSourceIsWebp} style="display: none"
                     src="data:image/webp;base64,UklGRiIAAABXRUJQVlA4IBYAAAAwAQCdASoBAAEADsD+JaQAA3AAAAAA"
                     onload="document.documentElement.classList.add('webp')"
                     onerror="document.documentElement.classList.add('no-webp')"/>
                <div class="neos-modal-centered">
                    <main class="neos-login-main">
                        <div class={['neos-login-box', props.backgroundImageSource ? 'background-image-active' : null]}>
                            <figure class="neos-login-box-logo">
                                <img class="neos-login-box-logo-resource" @children="attributes.src" width="200px"
                                     height="200px" alt="Neos Logo">
                                <Neos.Fusion:ResourceUri path="resource://Neos.Neos/Public/Images/Login/Logo.svg"/>
                                </img>
                            </figure>

                            <h1 class="neos-login-heading">
                                <b>{props.teaserTitle}</b>
                            </h1>

                            <h1 class="neos-login-heading">
                                {props.teaserText}
                            </h1>

                            <div class="neos-login-body neos">
                                <Neos.Fusion.Form:Form form.target.action="create">
                                    <Neos.Fusion.Form:Hidden field.name="identifier" field.value={sessionIdentifier}/>
                                    <Neos.Fusion.Form:Hidden field.name="secret" field.value={secret}/>

                                    <div class="neos-control-group">
                                        <img src={qrCode} style="width: 100%; max-width: 400px"/>
                                    </div>

                                    <div class="neos-control-group">
                                        <Neos.Fusion.Form:Input
                                            field.name="secondFactorFromApp"
                                            attributes.required="required"
                                            attributes.id="secondFactorFromApp"
                                            attributes.placeholder={I18n.id('otp-placeholder').package('Sandstorm.NeosTwoFactorAuthentication')}
                                            attributes.class="neos-span5"
                                            attributes.aria-label={I18n.id('otp-placeholder').package('Sandstorm.NeosTwoFactorAuthentication')}
                                            attributes.autocomplete="off"
                                        />
                                    </div>

                                    <div class="neos-actions">
                                        <Neos.Fusion.Form:Button attributes.class="neos-button neos-login-btn">
                                            {I18n.id('module.new.submit-otp').package('Sandstorm.NeosTwoFactorAuthentication').source('Backend').translate()}
                                        </Neos.Fusion.Form:Button>
                                    </div>
                                </Neos.Fusion.Form:Form>

                                <Neos.Fusion:Loop items={props.flashMessages} itemName="flashMessage">
                                    <div
                                        class={'neos-tooltip neos-bottom neos-in neos-tooltip-' + props.severityMapping[flashMessage.severity]}>
                                        <script @if.error={flashMessage.severity == 'Error'}>
                                            {
                                                "document.querySelector('fieldset').classList.add('effect--shake');"
                                            }
                                        </script>
                                        <div class="neos-tooltip-arrow"></div>
                                        <div class="neos-tooltip-inner" role="alert">
                                            {flashMessage}
                                        </div>
                                    </div>
                                </Neos.Fusion:Loop>
                            </div>
                        </div>
                    </main>
                    <footer class="neos-login-footer">
                        <p>
                            <a href="http://neos.io" target="_blank">Neos</a> –&nbsp;
                            © 2006-{Date.format('now', 'Y')} This is free software, licensed under GPL3 or higher, and
                            you are welcome to redistribute it under certain conditions;&nbsp;
                            Neos comes with ABSOLUTELY NO WARRANTY;&nbsp;
                            See <a href="http://neos.io" target="_blank">neos.io</a> for more details. Obstructing the
                            appearance of this notice is prohibited by law.
                        </p>
                    </footer>
                </div>

                <script>{"
                                                                                                        document.querySelector('form').addEventListener('submit', function() {
                                                                                                            document.querySelector('.neos-login-btn').classList.toggle('neos-hidden');
                                                                                                            document.querySelector('.neos-login-btn.neos-disabled').classList.toggle('neos-hidden');
                                                                                                        });
                                                                                                        try {
                                                                                                            document.querySelector('form[name=\"login\"] input[name=\"lastVisitedNode\"]').value = sessionStorage.getItem('Neos.Neos.lastVisitedNode');
                                                                                                        } catch(e) {}
                                                                                                    "}</script>
                </body>
                </html>
            `
        }
    }
}

